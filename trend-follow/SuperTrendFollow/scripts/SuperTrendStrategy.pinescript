//@version=5
//@Author: @GeorgeFreelanceDeveloper, @LucyFreelanceDeveloper
strategy("Supertrend Strategy", overlay=true, default_qty_type=strategy.percent_of_equity, default_qty_value=15)
import TradingView/Strategy/3

// ********************************
// User defined inputs
// ********************************

var G_BASIC_SETTINGS = "Basic settings"
var G_FILTER_SETTINGS  = "Filter settings"
var G_TIME = "Time settings"

atrPeriod = input.int(defval = 10, title = "ATR Length", group = G_BASIC_SETTINGS)
factor = input.float(defval = 3.0, title = "Factor", step = 0.01, group = G_BASIC_SETTINGS)
riskPercentage = input.float(defval = 1.00, title = "Risk per trade", step = 0.01, group = G_BASIC_SETTINGS)

enableFilter = input.bool(defval = true, title = "Enable filter", tooltip = "Filter is close price is above 200 day simple moving average of benchmark",  group = G_FILTER_SETTINGS)
benchmarkSymbol = input.string(defval = "SPX", title = "Benchmark", group = G_FILTER_SETTINGS)

fromDateTime = input.time(defval = timestamp("2012-01-01T01:01+0000"), title = "From Date and time", group = G_TIME) 
toDateTime = input.time(defval = timestamp("9999-01-01T01:01+0000"), title = "To Date and time", group = G_TIME)


// ********************************
// Functions
// ********************************
tradeDateIsAllowed() => time >= fromDateTime and time <= toDateTime

calculateQuantity(entryPrice, stopLossPrice, riskPercentage) =>
    move = math.abs(entryPrice-stopLossPrice)
    qty = ((riskPercentage/100)*strategy.equity) / move


// **********************************
// Perform calculations and analysis
// **********************************

// Filter
benchmarkSymbolClose = request.security(benchmarkSymbol,"1D", close)
filter = enableFilter ? benchmarkSymbolClose[1] >= ta.sma(source = benchmarkSymbolClose, length = 200) : true

[superTrendValue, direction] = ta.supertrend(factor, atrPeriod)

longCondition = ta.change(direction) < 0 and filter and tradeDateIsAllowed() // ta.change(direction) < 0 pokud ta.change(direction) vraci -1 jdeme do longu
exitCondition = ta.change(direction) > 0 and tradeDateIsAllowed() // ta.change(direction) > 0 pokud ta.change(direction) vraci 1 jdeme do shortu

// ********************************
// Draw outputs
// ********************************
//plot(strategy.equity, title="equity", color=color.red, linewidth=2, style=plot.style_areabr)


// ********************************
// Manage trade
// ********************************
if (longCondition)
    strategy.entry("My Long Entry Id", strategy.long,qty = calculateQuantity(close,superTrendValue,riskPercentage))

if (exitCondition)
    strategy.close("My Long Entry Id")